plugins {
    id "io.freefair.lombok" version "8.10" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
}

allprojects {
    version = project.project_version
    group = project.maven_group

    repositories {
        mavenCentral()
        maven { url "https://maven.mrnavastar.me/releases"}
        maven { url "https://maven.minecraftforge.net" }                    // Forge
        maven { url "https://maven.neoforged.net/releases/" }
        maven { url "https://repo.papermc.io/repository/maven-public/" }    // Paper + Velocity
        maven { url "https://api.modrinth.com/maven" }                      // Modrinth
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.javaVersion as Integer
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"
    apply plugin: "architectury-plugin"

    dependencies {
        compileOnly "me.mrnavastar.protoweaver:common:${project.protoweaver_version}"
        compileOnly "org.apache.fury:fury-core:${project.fury_version}"
        compileOnly "me.mrnavastar:r:${project.r_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    task jarSources(type:Jar){
        archiveClassifier.set("sources")
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }

    jar.finalizedBy(jarSources)
}